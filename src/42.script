Window.SetBackgroundTopColor (0.00, 0.00, 0.00);
Window.SetBackgroundBottomColor (0.00, 0.00, 0.00);

screen_ratio = Window.GetHeight() / Window.GetWidth();

for (i = 0; i < 54; i++)
	monolythe_image[i] = Image("progress-" + i + ".png");

/* Check if screen ratio is taller than image ratio, we will match the screen height */
monolythe_image_ratio = monolythe_image[0].GetHeight() / monolythe_image[0].GetWidth();
if (screen_ratio > monolythe_image_ratio)
{
	scale_factor = Window.GetHeight() / monolythe_image[0].GetHeight();
	for (i = 0; i < 54; i++)
		monolythe_image[i] = monolythe_image[i].Scale(monolythe_image[i].GetWidth() * scale_factor,
														monolythe_image[i].GetHeight() * scale_factor);
}

campus_image = Image("campus.png");
campus_image_ratio = campus_image.GetHeight() / campus_image.GetWidth();
campus_image = campus_image.Scale(campus_image.GetWidth() * 0.5, campus_image.GetHeight() * 0.5);

monolythe_sprite_left = Sprite();
monolythe_sprite_right = Sprite();
campus_sprite = Sprite();

campus_sprite.SetX(Window.GetWidth() / 2 - campus_image.GetWidth() / 2);
campus_sprite.SetY(Window.GetHeight() / 4 - campus_image.GetHeight() / 2);
campus_sprite.SetImage(campus_image);

monolythe_sprite_left.SetX(0); /* Place in the left */
monolythe_sprite_left.SetY(Window.GetHeight() / 2 - monolythe_image[0].GetHeight() / 2);

monolythe_sprite_right.SetX(Window.GetWidth() - monolythe_image[0].GetWidth()); /* Place in the right */
monolythe_sprite_right.SetY(Window.GetHeight() / 2 - monolythe_image[0].GetHeight() / 2);

progress = 0;

fun refresh_callback ()
{
	monolythe_sprite_left.SetImage(monolythe_image[Math.Int(progress / 3) % 54]);
	monolythe_sprite_right.SetImage(monolythe_image[Math.Int(progress / 3) % 54]);
	progress++;
}

Plymouth.SetRefreshFunction (refresh_callback);

status = "normal";

fun dialog_setup()
{
	local.box;
	local.lock;
	local.entry;

	box.image = Image("box.png");
	lock.image = Image("lock.png");
	entry.image = Image("entry.png");

	box.sprite = Sprite(box.image);
	box.x = Window.GetWidth()  / 2 - box.image.GetWidth ()/2;
	box.y = Window.GetHeight() / 2 - box.image.GetHeight()/2;
	box.z = 10000;
	box.sprite.SetPosition(box.x, box.y, box.z);

	lock.sprite = Sprite(lock.image);
	lock.x = box.x + box.image.GetWidth()/2 - (lock.image.GetWidth() + entry.image.GetWidth()) / 2;
	lock.y = box.y + box.image.GetHeight()/2 - lock.image.GetHeight()/2;
	lock.z = box.z + 1;
	lock.sprite.SetPosition(lock.x, lock.y, lock.z);

	entry.sprite = Sprite(entry.image);
	entry.x = lock.x + lock.image.GetWidth();
	entry.y = box.y + box.image.GetHeight()/2 - entry.image.GetHeight()/2;
	entry.z = box.z + 1;
	entry.sprite.SetPosition(entry.x, entry.y, entry.z);

	global.dialog.box = box;
	global.dialog.lock = lock;
	global.dialog.entry = entry;
	global.dialog.bullet_image = Image("bullet.png");
	dialog_opacity (1);
}

fun display_normal_callback ()
  {
	global.status = "normal";
	if (global.dialog)
		dialog_opacity (0);
  }

fun display_password_callback (prompt, bullets)
{
	global.status = "password";
	if (!global.dialog)
		dialog_setup();
	else
		dialog_opacity(1);
	for (index = 0; dialog.bullet[index] || index < bullets; index++)
	{
		if (!dialog.bullet[index])
		{
			dialog.bullet[index].sprite = Sprite(dialog.bullet_image);
			dialog.bullet[index].x = dialog.entry.x + index * dialog.bullet_image.GetWidth();
			dialog.bullet[index].y = dialog.entry.y + dialog.entry.image.GetHeight() / 2 - dialog.bullet_image.GetHeight() / 2;
			dialog.bullet[index].z = dialog.entry.z + 1;
			dialog.bullet[index].sprite.SetPosition(dialog.bullet[index].x, dialog.bullet[index].y, dialog.bullet[index].z);
		}
		if (index < bullets)
			dialog.bullet[index].sprite.SetOpacity(1);
		else
			dialog.bullet[index].sprite.SetOpacity(0);
	}
}

Plymouth.SetDisplayNormalFunction(display_normal_callback);
Plymouth.SetDisplayPasswordFunction(display_password_callback);